buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        kotlinOptions.freeCompilerArgs += ['-module-name', "$POM_ARTIFACT_ID"]
    }
}

dependencies {
    // Android
    implementation "com.android.support:support-annotations:${supportLibVersion}"

    implementation "com.android.support:appcompat-v7:${supportLibVersion}" // used by org.sqlite.sqliteX:sqlite-android
    implementation "com.android.support:support-media-compat:${supportLibVersion}" // used by org.sqlite.sqliteX:sqlite-android
    implementation "com.android.support:support-vector-drawable:${supportLibVersion}" // used by org.sqlite.sqliteX:sqlite-android
    implementation "com.android.support:animated-vector-drawable:${supportLibVersion}" // used by org.sqlite.sqliteX:sqlite-android

    compileOnly "android.arch.lifecycle:runtime:$archVersion"
    compileOnly "android.arch.lifecycle:extensions:$archVersionEA"
    implementation "android.arch.persistence.room:runtime:$archVersionEA"

    // optional
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compileOnly 'org.sqlite.sqliteX:sqlite-android:3.15.2' // SQL Android Bindings

    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
}

// =============== Maven Deployment ===============
// ./gradlew clean check installLocal
task installLocal(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository (url: "file://${System.env.HOME}/.m2/repository/")
            pom.groupId = GROUP
            pom.artifactId = POM_ARTIFACT_ID
            pom.version = VERSION_NAME
            pom.packaging = POM_PACKAGING
            uniqueVersion = false // don't put in a timestamp on the filename (other project will not be able to depend on it)
        }
    }
}

// DEPLOY to Maven Central (this must be the last line of this file)
// ./gradlew clean check uploadArchives
apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
