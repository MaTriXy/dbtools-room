buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        kotlinOptions.freeCompilerArgs += ['-module-name', "$POM_ARTIFACT_ID"]
    }
    buildToolsVersion '27.0.3'
}

dependencies {
    compileOnly "android.arch.lifecycle:runtime:$archVersionLifecycleRT"
    compileOnly "android.arch.lifecycle:extensions:$archVersion"
    implementation "android.arch.persistence.room:runtime:$archVersion"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.13.0'
}

// =============== Maven Deployment ===============
// ./gradlew clean check installLocal
task installLocal(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository (url: "file://${System.env.HOME}/.m2/repository/")
            pom.groupId = GROUP
            pom.artifactId = POM_ARTIFACT_ID
            pom.version = VERSION_NAME
            pom.packaging = POM_PACKAGING
            uniqueVersion = false // don't put in a timestamp on the filename (other project will not be able to depend on it)
        }
    }
}

// DEPLOY to Maven Central (this must be the last line of this file)
// ./gradlew clean check uploadArchives
apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
